%{
    #include "parser.hpp"
    #include "output.hpp"

    #include <iostream>
    using namespace std;
    int yylex();
    void yyerror(char const*);
    extern int yylineno;
    /* For debug */
    // extern char* yytext;
%}

%token VOID INT BYTE B BOOL TRUE FALSE RETURN IF WHILE BREAK CONTINUE SC COMMA LPAREN RPAREN LBRACE RBRACE ID NUM STRING

%right ASSIGN
%left OR
%left AND
%right NOT

%nonassoc RELOP
%nonassoc BINOP

%left LEFT
%nonassoc NOELSE
%nonassoc ELSE

%%

Program :
    Funcs                           {output::printProductionRule(1);}
;

Funcs :
    %empty                          {output::printProductionRule(2);}
    |   FuncDecl Funcs              {output::printProductionRule(3);}
;

FuncDecl:
    RetType ID LPAREN Formals RPAREN LBRACE Statements RBRACE {output::printProductionRule(4);}
;

RetType:
    Type                            {output::printProductionRule(5);}
    |   VOID                        {output::printProductionRule(6);}
;

Formals:
    %empty                          {output::printProductionRule(7);}
    | FormalsList                   {output::printProductionRule(8);}
;

FormalsList:
    FormalDecl                      {output::printProductionRule(9);}
    | FormalDecl COMMA FormalsList  {output::printProductionRule(10);}
;

FormalDecl:
    Type ID                         {output::printProductionRule(11);}
;

Statements:
    Statement                       {output::printProductionRule(12);}
    | Statements Statement          {output::printProductionRule(13);}
;

Statement:
    LBRACE Statements RBRACE       {output::printProductionRule(14);}
    | Type ID SC                      {output::printProductionRule(15);}
    | Type ID ASSIGN Exp SC         {output::printProductionRule(16);}
    | ID ASSIGN Exp SC              {output::printProductionRule(17);}
    | Call SC                       {output::printProductionRule(18);}
    | RETURN SC                     {output::printProductionRule(19);}
    | RETURN Exp SC                 {output::printProductionRule(20);}
    | IF LPAREN Exp RPAREN Statement %prec NOELSE              {output::printProductionRule(21);}
    | IF LPAREN Exp RPAREN Statement ELSE Statement            {output::printProductionRule(22);}
    | WHILE LPAREN Exp RPAREN Statement %prec NOELSE           {output::printProductionRule(23);}
    | WHILE LPAREN Exp RPAREN Statement ELSE Statement         {output::printProductionRule(24);}
    | BREAK SC                      {output::printProductionRule(25);}
    | CONTINUE SC                   {output::printProductionRule(26);}
;

Call:
    ID LPAREN ExpList RPAREN        {output::printProductionRule(27);}
    |   ID LPAREN RPAREN            {output::printProductionRule(28);}
;

ExpList:
    Exp                             {output::printProductionRule(29);}
    | Exp COMMA ExpList             {output::printProductionRule(30);}
;

Type:
    INT                             {output::printProductionRule(31);}
    |   BYTE                        {output::printProductionRule(32);}
    |   BOOL                        {output::printProductionRule(33);}
;

Exp:
    LPAREN Exp RPAREN %prec LEFT               {output::printProductionRule(34);}
    | Exp BINOP Exp                 {output::printProductionRule(35);}
    | ID                            {output::printProductionRule(36);}
    | Call                          {output::printProductionRule(37);}
    | NUM                           {output::printProductionRule(38);}
    | NUM B                         {output::printProductionRule(39);}
    | STRING                        {output::printProductionRule(40);}
    | TRUE                          {output::printProductionRule(41);}
    | FALSE                         {output::printProductionRule(42);}
    | NOT Exp                       {output::printProductionRule(43);}
    | Exp AND Exp                   {output::printProductionRule(44);}
    | Exp OR Exp                    {output::printProductionRule(45);}
    | Exp RELOP Exp                 {output::printProductionRule(46);}
;

%%

int main() {
    return yyparse();
}

void yyerror(char const* s) {
    output::errorSyn(yylineno);
    /* For debug */
    /* cout << "syntax error from token: " << yytext << endl; */
    exit(1);
}